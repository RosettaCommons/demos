#!/usr/bin/awk -f
#
# makes mutants as specified in .csv file

function usage(errmsg) {
  if(errmsg != "")
    print "error: " errmsg > "/dev/stderr";
  print "usage: mkmut-csv [--multi|--single] <file.csv> ..." > "/dev/stderr";
  print "makes mutants as specified in file.csv" > "/dev/stderr";
  print "  --multi: makes multiple mutants only" > "/dev/stderr";
  print "  --single: makes point mutations only" > "/dev/stderr";
  bail = 1;
  exit;
}

BEGIN {
  FS = ",";
  i = 1;
  outmode = 0;  # 0 = multi & single; 1 = single only; 2 = multi only
  while(i < ARGC && ARGV[i] ~ /^-/) {
    if(ARGV[i] ~ /-+usage/) {
      usage();
    } else if(ARGV[i] == "--multi") {
      if(outmode != 0)
	usage("cannot specify both --single and --multi");
      outmode = 2;
      delete ARGV[i];
      i++;
    } else if(ARGV[i] == "--single") {
      if(outmode != 0)
	usage("cannot specify both --single and --multi");
      outmode = 1;
      delete ARGV[i];
      i++;
    } else {
      usage("unknown argument " ARGV[i]);
    }
  }
  inmode = 0;  # assigned in /^Name/: 0 = combined, 1 = single only
}
/^\#/ { next; }
/^(species|corresp)/ { next; }
/^Name/ {
  if($1 == "Name+")
    inmode = 1;
  else
    inmode = 0;
  if(inmode == 1 && outmode == 2) {
    print FILENAME ": --multi specified, but no multiple mutations in file, skipping";
    nextfile;
  }
}
!/^Name/ {
  if($2 == "wt")   # nothing to do for wt
    next;
  if($1 ~ /^+/) {
    # single
    name_in = substr($1, 2);
    sample_mode = 1;
  } else {
    # multi
    name_in = $1;
    sample_mode = 2;
  }
  if(sample_mode == 1 && outmode == 2)
    next;
  if(sample_mode == 2 && outmode == 1)
    next;
  base = gensub("-[^-]*$", "", 1, name_in);
  fn_in = base ".pdb";
  if(sample_mode == 1) {
    mut_in = $3;
    resi = gensub(".$", "", 1, mut_in);
    resn = gensub("[[:alpha:]][[:digit:]]+", "", 1, mut_in);
    cmd = "pymut.sh " fn_in " " resi " " resn;
  } else if(sample_mode == 2) {
    mut_list = "";
    for(i = 3; i <= NF; i++)
      if($i != "")
	mut_list = mut_list " " $i;
    fn_out = name_in ".pdb";
    cmd = "pymut-multi.sh " fn_in " " fn_out mut_list;
  }
  print "running: " cmd;
  r = system(cmd);
  if(r != 0)
    print "ERROR: " FILENAME " line " NR;
}
END {
  if(!bail)
    ;
}
